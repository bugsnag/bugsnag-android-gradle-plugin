// Gradle plugins
plugins {
    id "groovy"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.6"
}

// Repositories for dependencies
repositories {
    jcenter()
}

// Target Java 1.6 when compiling groovy code
compileGroovy {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

// Build dependencies
dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:1.5.0'
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile 'org.apache.httpcomponents:httpmime:4.3.6'
}

// Tasks to build jars for docs and sources
task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// Publishing settings
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'bugsnag'
        version {
            attributes = ['gradle-plugin': "${project.property('gradle.plugin.id')}:${project.group}:${project.name}"]
            vcsTag = "v${project.version}"

            gpg {
                sign = true
            }

            mavenCentralSync {
                sync = true
                user = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}
