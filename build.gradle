buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
    }
}

// Gradle plugins
plugins {
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'com.jfrog.bintray' version '1.7.3'
}
apply plugin: 'maven-publish'


allprojects {
    repositories {
        jcenter()
        google()
    }
}

// Repositories for dependencies
repositories {
    jcenter()
    google()
}

// Target Java 1.6 when compiling groovy code
compileGroovy {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

// Build dependencies
dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:3.1.0'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'org.apache.httpcomponents:httpmime:4.5.2'
    testCompile 'junit:junit:4.12'
}

// Maven publishing settings (nexus-maven-plugins)
modifyPom {
    project {
        name 'Bugsnag Android Gradle Plugin'
        description 'Gradle plugin to automatically upload ProGuard mapping files to Bugsnag.'
        url 'https://github.com/bugsnag/bugsnag-android-gradle-plugin'

        scm {
            url 'https://github.com/bugsnag/bugsnag-android-gradle-plugin'
            connection 'scm:git:git://github.com/bugsnag/bugsnag-android-gradle-plugin.git'
            developerConnection 'scm:git:ssh://git@github.com/bugsnag/bugsnag-android-gradle-plugin.git'
        }

        licenses {
            license {
                name 'MIT'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        organization {
            name 'Bugsnag'
            url 'https://bugsnag.com'
        }

        developers {
            developer {
                id 'loopj'
                name 'James Smith'
                email 'james@bugsnag.com'
            }
        }
    }
}

// Gradle plugin publishing settings (plugins.gradle.com)
pluginBundle {
    website = 'https://github.com/bugsnag/bugsnag-android-gradle-plugin'
    vcsUrl = 'https://github.com/bugsnag/bugsnag-android-gradle-plugin.git'

    plugins {
        androidGradlePlugin {
            id = 'com.bugsnag.android.gradle'
            displayName = 'Bugsnag Android Gradle Plugin'
            description = 'Gradle plugin to automatically upload ProGuard mapping files to Bugsnag.'
            tags = ['bugsnag', 'proguard', 'android', 'upload']
        }
    }

    mavenCoordinates {
        groupId = 'com.bugsnag'
        artifactId = 'bugsnag-android-gradle-plugin'
    }
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifact jar
            groupId 'com.bugsnag'
            artifactId 'bugsnag-android-gradle-plugin'
            version project.version
        }
    }
}

// Bintray upload
bintray {
    user = project.hasProperty("bintray_user") ? "$bintray_user" : System.getenv("bintray_user")
    key = project.hasProperty("bintray_api_key") ? "$bintray_api_key" : System.getenv("bintray_api_key")

    publications = ['Publication']
    pkg {
        repo = 'maven'
        name = 'bugsnag-android-gradle-plugin'
        userOrg = 'bugsnag'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bugsnag/bugsnag-android-gradle-plugin.git'
        version {
            name = project.version
            vcsTag = "v${project.version}"
            attributes = ['gradle-plugin': "com.bugsnag.android.gradle"]
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
